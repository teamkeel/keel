model User {
    fields {
        identity Identity @unique
        name Text
        orgs UserOrganisation[]
        status Status @default(Status.Active)
    }

    actions {
        create createUser() with (name) @function
        get getUser(id) @function
        list listUsersByOrganisation(orgs.org.id) @function
        delete deleteUser(id) {
            @function
            @permission(expression: user.status == Status.Active)
        }
    }

    @permission(
        expression: ctx.identity == user.identity,
        actions: [create, get]
    )

    @permission(
        expression: ctx.identity.user in user.orgs.org.users.user,
        actions: [get, list]
    )
}

enum Status {
    Active
    Inactive
}

model Organisation {
    fields {
        name Text
        users UserOrganisation[]
    }
}

model UserOrganisation {
    fields {
        user User
        org Organisation
    }

    @unique([user, org])
}

model Film {
    fields {
        title Text
        ageRestriction Number
    }
}

model Admission {
    fields {
        film Film
        audience Audience
    }

    actions {
        create createAdmission() with (film.id) @function
    }

    // Critics can always watch films, unless they work for the Daily Mail
    @permission(
        expression: ctx.identity.audience.isCritic && ctx.identity.audience.publication.name != "Daily Mail",
        actions: [create]
    )

    // Otherwise the audience member needs to be old enough to view the film
    @permission(
        expression: ctx.identity.audience.age >= admission.film.ageRestriction,
        actions: [create]
    )
}

model Audience {
    fields {
        identity Identity @unique
        isCritic Boolean
        age Number
        publication Publication?
    }
}

model Publication {
    fields {
        name Text
    }
}

// Model to track if hooks were executed
model HookLog {
    fields {
        actionName Text
        hookName Text
        executedAt Timestamp
    }
}

model Post {
    fields {
        title Text
        author User
    }

    actions {
        // Create with schema permission that requires identity
        create createPostWithHook() with (title, author.id) {
            @function
            @permission(expression: ctx.isAuthenticated)
        }
        // Get with schema permission that requires identity
        get getPostWithHook(id) {
            @function
            @permission(expression: ctx.isAuthenticated)
        }
        // List with schema permission that requires identity
        list listPostsWithHook() {
            @function
            @permission(expression: ctx.isAuthenticated)
        }
        // Update with schema permission that requires identity
        update updatePostWithHook(id) with (title) {
            @function
            @permission(expression: ctx.isAuthenticated)
        }
        // Delete with schema permission that requires identity
        delete deletePostWithHook(id) {
            @function
            @permission(expression: ctx.isAuthenticated)
        }
    }
}
